apply plugin: 'com.android.application'
apply plugin: "io.fabric"
apply plugin: "com.github.ben-manes.versions"

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://maven.fabric.io/public"
        }
    }

    dependencies {
        classpath "io.fabric.tools:gradle:1.25.1"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.17.0"
    }
}

repositories {
    maven { url "https://maven.fabric.io/public" }
}

def libraries = rootProject.ext.libraries
def config = rootProject.ext.config

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def productVersion = "$versionMajor.$versionMinor.$versionPatch"

def static gitBuildNumber() {
    return "git rev-list HEAD --count --no-merges".execute().text.trim().toInteger()
}

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        applicationId "com.leaguechampions"
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            File projectProperties = file("../../../keystores/LeagueChampions/leaguechampions.properties")
            if (projectProperties.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(projectProperties))

                storeFile file(props["keystore.path"])
                storePassword props["keystore.password"]
                keyAlias props["keystore.key.alias"]
                keyPassword props["keystore.key.password"]
            }
        }
        debug {
            File keystoreDebug = file("../debug.keystore")
            if (keystoreDebug.exists()) {
                storeFile keystoreDebug
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            useProguard true
            signingConfig signingConfigs.release
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            debuggable true
            minifyEnabled false
            useProguard false
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }
    }

    flavorDimensions "server"

    productFlavors {
        prod {
            dimension "server"
            def versionBuild = gitBuildNumber()
            versionCode versionBuild
            versionName "${productVersion}.${versionBuild}"
            resValue "string", "app_name", "League Champions"
        }
        dev {
            dimension "server"
            applicationIdSuffix ".dev"
            versionCode 0
            versionName "${productVersion}-DEV"
            resValue "string", "app_name", "DEV - League Champions"
            multiDexEnabled true
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = output.outputFileName
                    .replace("app", "LeagueChampions")
                    .replace(".apk", variant.buildType.name == "release" ? "-${productVersion}." + gitBuildNumber() + ".apk" : "-${productVersion}.apk")
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == "debug") {
            if (variant.getFlavors().get(0).name == "prod") {
                variant.setIgnore(true)
            }
        } else if (variant.buildType.name == "release") {
            if (variant.getFlavors().get(0).name == "dev") {
                variant.setIgnore(true)
            }
        }
    }

    dexOptions {
        maxProcessCount 8
        javaMaxHeapSize "2g"
        preDexLibraries true
        dexInProcess true
    }

    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
        exclude ".readme"
    }

    lintOptions {
        abortOnError true
        xmlReport false
        htmlReport true
        warning "InvalidPackage"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // SUPPORT
    implementation "com.android.support:design:$libraries.supportLib"
    implementation "com.android.support:appcompat-v7:$libraries.supportLib"
    implementation "com.android.support:recyclerview-v7:$libraries.supportLib"
    implementation "com.android.support:support-compat:$libraries.supportLib"
    implementation "com.android.support:support-core-utils:$libraries.supportLib"
    implementation "com.android.support:support-core-ui:$libraries.supportLib"
    implementation "com.android.support:support-fragment:$libraries.supportLib"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // ARCH
    implementation "android.arch.lifecycle:extensions:$libraries.arch"
    annotationProcessor "android.arch.lifecycle:compiler:$libraries.arch"
    testImplementation "android.arch.core:core-testing:$libraries.arch"

    // DAGGER
    implementation "com.google.dagger:dagger:$libraries.dagger"
    implementation "com.google.dagger:dagger-android:$libraries.dagger"
    implementation "com.google.dagger:dagger-android-support:$libraries.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$libraries.dagger"
    annotationProcessor "com.google.dagger:dagger-android-processor:$libraries.dagger"

    // GSON
    implementation "com.google.code.gson:gson:$libraries.gson"

    // OKIO
    implementation "com.squareup.okio:okio:$libraries.okio"

    // OKHTTP
    implementation "com.squareup.okhttp3:okhttp:$libraries.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$libraries.okhttp"

    // RETROFIT
    implementation "com.squareup.retrofit2:retrofit:$libraries.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$libraries.retrofit"
    debugImplementation "com.squareup.retrofit2:retrofit-mock:$libraries.retrofit"

    // PICASSO
    implementation "com.squareup.picasso:picasso:$libraries.picasso"
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:$libraries.picasso_okhttp"

    implementation "com.github.bumptech.glide:glide:$libraries.glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$libraries.glide_okhttp"

    // BUTTER KNIFE
    implementation "com.jakewharton:butterknife:$libraries.butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$libraries.butterknife"

    // TIMBER
    implementation "com.jakewharton.timber:timber:$libraries.timber"

    // CRASHLYTICS
    implementation("com.crashlytics.sdk.android:crashlytics:$libraries.crashlytics@aar") {
        transitive = true
    }

    // LEAK CANARY
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$libraries.leakcanary"

    // TESTS
    testImplementation "junit:junit:$libraries.junit"
    testImplementation "org.powermock:powermock-module-junit4:$libraries.powermock"
    testImplementation "org.powermock:powermock-core:$libraries.powermock"
    testImplementation "org.powermock:powermock-api-mockito2:$libraries.powermock"
    testImplementation "org.mockito:mockito-core:$libraries.mockito"
    testImplementation("org.robolectric:robolectric:$libraries.robolectric") {
        exclude group: "commons-logging", module: "commons-logging"
        exclude group: "org.apache.httpcomponents", module: "httpclient"
    }

    testImplementation "org.assertj:assertj-core:$libraries.assertj"
    testImplementation "org.hamcrest:hamcrest-library:$libraries.hamcrest"

    androidTestImplementation "com.android.support.test:runner:$libraries.supportTest"
    androidTestImplementation "com.android.support.test:rules:$libraries.supportTest"
    androidTestImplementation "com.android.support:support-annotations:$libraries.supportLib"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$libraries.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$libraries.espresso"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$libraries.espresso") {
        exclude group: "com.android.support", module: "appcompat-v7"
        exclude group: "com.android.support", module: "design"
        exclude group: "com.android.support", module: "support-v4"
        exclude module: "recyclerview-v7"
    }
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.1'
}

apply plugin: "com.google.gms.google-services"