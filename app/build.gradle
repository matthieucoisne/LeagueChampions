apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"
apply plugin: "io.fabric"
apply plugin: "com.github.ben-manes.versions"

def libraries = rootProject.ext.libraries
def config = rootProject.ext.config

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def productVersion = "$versionMajor.$versionMinor.$versionPatch"

def static getCommitCount() {
    return "git rev-list HEAD --count --no-merges".execute().text.trim().toInteger()
}

def static getBuildNumber() {
    def buildNumber = System.getenv("BITRISE_BUILD_NUMBER")
    return buildNumber != null ? buildNumber.toInteger() : 0
}

android {
    compileSdkVersion config.compileSdk
    buildToolsVersion config.buildTools

    defaultConfig {
        applicationId "com.leaguechampions"
        minSdkVersion config.minSdk
        targetSdkVersion config.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            File projectProperties = file("../../../keystores/LeagueChampions/leaguechampions.properties")
            if (projectProperties.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(projectProperties))

                storeFile file(props["keystore.path"])
                storePassword props["keystore.password"]
                keyAlias props["keystore.key.alias"]
                keyPassword props["keystore.key.password"]
            }
        }
        debug {
            File keystoreDebug = file("../debug.keystore")
            if (keystoreDebug.exists()) {
                storeFile keystoreDebug
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            useProguard true
            signingConfig signingConfigs.release
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            debuggable true
            minifyEnabled false
            useProguard false
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }
    }

    flavorDimensions "server"
    productFlavors {
        prod {
            dimension "server"
            def versionBuild = getCommitCount()
            versionCode versionBuild
            versionName "$productVersion.$versionBuild"
            resValue "string", "app_name", "League Champions"
        }
        dev {
            dimension "server"
            applicationIdSuffix ".dev"
            versionCode 0
            versionName "$productVersion-DEV"
            resValue "string", "app_name", "DEV - League Champions"
            multiDexEnabled true
        }
    }

    variantFilter { variant ->
        def buildType = variant.buildType.name
        def flavor = variant.flavors.get(0).name
        if (buildType == "debug") {
            if (flavor == "prod") {
                variant.setIgnore(true)
            }
        } else if (buildType == "release") {
            if (flavor == "dev") {
                variant.setIgnore(true)
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = output.outputFileName
                    .replace("app", "LeagueChampions")
                    .replace(".apk", variant.buildType.name == "release" ? "-${productVersion}." + getCommitCount() + ".apk" : "-${productVersion}.apk")
        }
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        maxProcessCount 8
        javaMaxHeapSize "2g"
        preDexLibraries true
        dexInProcess true
    }

    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
        exclude ".readme"
    }

    lintOptions {
        abortOnError true
        xmlReport false
        htmlReport true
        warning "InvalidPackage"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ["alpha", "beta", "rc", "cr", "m"].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject("Release candidate")
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$config.kotlin"

    // ANDROIDX
    implementation "com.google.android.material:material:$libraries.androidx"
    implementation "androidx.core:core:$libraries.androidx_core"
    implementation "androidx.appcompat:appcompat:$libraries.appCompat"
    implementation "androidx.fragment:fragment:$libraries.androidx"
    implementation "androidx.recyclerview:recyclerview:$libraries.androidx"
    implementation "androidx.legacy:legacy-support-core-utils:$libraries.androidx"
    implementation "androidx.legacy:legacy-support-core-ui:$libraries.androidx"
    implementation "androidx.constraintlayout:constraintlayout:$libraries.constraintLayout"
    implementation "androidx.lifecycle:lifecycle-extensions:$libraries.arch"
    kapt "androidx.lifecycle:lifecycle-compiler:$libraries.arch"

    // DAGGER
    implementation "com.google.dagger:dagger:$libraries.dagger"
    implementation "com.google.dagger:dagger-android:$libraries.dagger"
    implementation "com.google.dagger:dagger-android-support:$libraries.dagger"
    kapt "com.google.dagger:dagger-compiler:$libraries.dagger"
    kapt "com.google.dagger:dagger-android-processor:$libraries.dagger"

    // RXJAVA
    implementation "io.reactivex.rxjava2:rxjava:$libraries.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$libraries.rxandroid"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$libraries.rxjava_adapter"

    // GSON
    implementation "com.google.code.gson:gson:$libraries.gson"

    // OKIO
    implementation "com.squareup.okio:okio:$libraries.okio"

    // OKHTTP
    implementation "com.squareup.okhttp3:okhttp:$libraries.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$libraries.okhttp"

    // RETROFIT
    implementation "com.squareup.retrofit2:retrofit:$libraries.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$libraries.retrofit"
    debugImplementation "com.squareup.retrofit2:retrofit-mock:$libraries.retrofit"

    // PICASSO
    implementation "com.squareup.picasso:picasso:$libraries.picasso"
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:$libraries.picasso_okhttp"

    implementation "com.github.bumptech.glide:glide:$libraries.glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$libraries.glide"

    // TIMBER
    implementation "com.jakewharton.timber:timber:$libraries.timber"

    // CRASHLYTICS
    implementation "com.crashlytics.sdk.android:crashlytics:$libraries.crashlytics"

    // LEAK CANARY
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$libraries.leakcanary"


    // UNIT TESTS
    testImplementation "androidx.arch.core:core-testing:$libraries.arch"

    testImplementation "junit:junit:$libraries.junit"

    testImplementation "org.mockito:mockito-core:$libraries.mockito"
    //http://static.javadoc.io/org.mockito/mockito-core/2.22.0/org/mockito/Mockito.html#0.2
    testImplementation "org.mockito:mockito-inline:$libraries.mockito"

    testImplementation("org.robolectric:robolectric:$libraries.robolectric") {
        exclude group: "commons-logging", module: "commons-logging"
        exclude group: "org.apache.httpcomponents", module: "httpclient"
    }

    testImplementation "org.assertj:assertj-core:$libraries.assertj"

    testImplementation "org.hamcrest:hamcrest-library:$libraries.hamcrest"


    // INSTRUMENTATION TESTS
    androidTestImplementation "androidx.test:runner:$libraries.androidxTest"
    androidTestImplementation "androidx.test:rules:$libraries.androidxTest"
    androidTestImplementation "androidx.annotation:annotation:$libraries.annotation"
    androidTestImplementation "androidx.test.espresso:espresso-core:$libraries.espresso"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$libraries.espresso"
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$libraries.espresso") {
        exclude group: "com.android.support", module: "appcompat-v7"
        exclude group: "com.android.support", module: "design"
        exclude group: "com.android.support", module: "support-v4"
        exclude module: "recyclerview-v7"
    }
}

apply plugin: "com.google.gms.google-services"
